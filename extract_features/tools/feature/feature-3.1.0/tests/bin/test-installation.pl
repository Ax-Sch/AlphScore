#!/usr/bin/perl

# ============================================================
# by Trevor Blackstone, San Francisco State University
# adapted from release candidate test system by Marc Sosnick
# Runs tests on FEATURE installation
# ============================================================

use lib qw( ./lib ../lib );
use Test::Harness qw( execute_tests );

our $total;
our $failed;
our $message;

$ENV{ FEATURE_DIR } = "../data";
$ENV{ PDB_DIR }     = "../data/pdb";
$ENV{ DSSP_DIR }    = "../data/dssp";

mkdir "testout" unless -e "testout";
read_messages();
run_installer_tests();

# ============================================================
sub read_messages {
# ============================================================
    my $content = '';
    my $context = '';
    while( <DATA> ) {
        if( /^== (\w+)/ ) {
            if( $content ) {
                $message->{ $context } = "\n$content";
                $content = '';
            }
            $context = lc $1;

        } else {
            $content .= $_;
        }
    }
    $message->{ $context } = "\n$content";
}

# ============================================================
sub run_installer_tests {
# ============================================================
    ($total, $failed) = execute_tests( tests => [ qw(
        tests/installer/trypsin-ser-og-positive.t
        tests/installer/trypsin-ser-og-negative.t
        tests/installer/trypsin-ser-og-model.t
    ) ] );
    if( $total->{ bad } != 0 ) {
        if( 
            exists $failed->{ 'tests/installer/trypsin-ser-og-positive.t' } ||
            exists $failed->{ 'tests/installer/trypsin-ser-og-negative.t' }
        ) {
            print $message->{ featurize };
        }
        if( exists $failed->{ 'tests/installer/trypsin-ser-og-model.t' } ) {
            print $message->{ model };
        }
        exit 1;
    }
}

__DATA__
== FEATURIZE
Feature vector generation failed!
  The featurize program does not perform as expected. This could be due
  to an improper installation environment or missing data files.

== MODEL
Model generation failed!
  The buildmodel program does not perform as expected. This could be a
  problem with the input data generated by the featurize program or it
  could be a run-time fault of buildmodel.

